import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'java'
}

ext {
    snakeYamlVersion = '2.2'
    shadowGradlePluginVersion = '8.1.1'
}

apply from: LOGSTASH_CORE_PATH + "/../rubyUtils.gradle"

// ===========================================================================
// plugin info
// ===========================================================================
group                      'org.logstashplugins' // must match the package of the main plugin class
version                    "${file("VERSION").text.trim()}" // read from required VERSION file
description                = "Output plugin for Vespa"
pluginInfo.licenses        = ['Apache-2.0'] // list of SPDX license IDs
pluginInfo.longDescription = "This gem is a Logstash plugin required to be installed on top of the Logstash core pipeline using \$LS_HOME/bin/logstash-plugin install gemname. This gem is not a stand-alone program"
pluginInfo.authors         = ['Radu Gheorghe']
pluginInfo.email           = ['radu@vespa.ai']
pluginInfo.homepage        = "https://vespa.ai"
pluginInfo.pluginType      = "output"
pluginInfo.pluginClass     = "VespaFeed"
pluginInfo.pluginName      = "vespa_feed" // must match the @LogstashPlugin annotation in the main plugin class
// ===========================================================================

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.runtimeClasspath, project.configurations.testRuntimeClasspath]
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.17.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.0'
    implementation fileTree(dir: LOGSTASH_CORE_PATH, include: "**/logstash-core.jar")

    implementation("com.yahoo.vespa:vespa-feed-client:${VESPA_VERSION}")

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'

    testImplementation 'org.jruby:jruby-complete:9.4.3.0'
    testImplementation 'org.jruby:jruby-core:9.4.3.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.9.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation("com.yahoo.vespa:vespa-feed-client:${VESPA_VERSION}")
    testImplementation fileTree(dir: LOGSTASH_CORE_PATH, include: "**/logstash-core.jar")
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.18.2'
}

clean {
    delete "${projectDir}/Gemfile"
    delete "${projectDir}/" + pluginInfo.pluginFullName() + ".gemspec"
    delete "${projectDir}/lib/"
    delete "${projectDir}/vendor/"
    new FileNameFinder().getFileNames(projectDir.toString(), pluginInfo.pluginFullName() + "-?.?.?.gem").each { filename ->
        delete filename
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register("vendor"){
    dependsOn shadowJar
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${pluginInfo.pluginFullName()}/${project.version}/${pluginInfo.pluginFullName()}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
        validatePluginJar(projectJarFile, project.group)
    }
}

tasks.register("generateRubySupportFiles") {
    doLast {
        generateRubySupportFilesForPlugin(project.description, project.group, version)
    }
}

tasks.register("removeObsoleteJars") {
    doLast {
        new FileNameFinder().getFileNames(
                projectDir.toString(),
                "vendor/**/" + pluginInfo.pluginFullName() + "*.jar",
                "vendor/**/" + pluginInfo.pluginFullName() + "-" + version + ".jar").each { f ->
            delete f
        }
    }
}

tasks.register("gem"){
    dependsOn = [downloadAndInstallJRuby, removeObsoleteJars, vendor, generateRubySupportFiles]
    doLast {
        buildGem(projectDir, buildDir, pluginInfo.pluginFullName() + ".gemspec")
    }
}

test {
    doFirst {
        println "Test runtime classpath:"
        classpath.each { println it }
    }
}

task integrationTest(type: Exec) {
    workingDir 'integration-test'
    commandLine './run_tests.sh'
}
