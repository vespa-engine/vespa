// This "noinspection" comment below is here to fix a warning
//noinspection GroovyAssignabilityCheck
plugins {
  id 'org.jetbrains.intellij' version '1.1.4'
  id 'java'

  id "org.jetbrains.grammarkit" version '2021.1.3'
  
  id 'maven-publish' // to deploy the plugin into a Maven repo
}

defaultTasks 'buildPlugin'

apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

task generateSdLexer(type: GenerateLexer) {
  source 'src/main/java/org/intellij/sdk/language/lexer/sd.flex'
  targetDir 'src/main/gen/org/intellij/sdk/language/lexer/'
  targetClass 'SdLexer'
  purgeOldFiles true
}

task generateSdParser(type: GenerateParser) {
  source 'src/main/java/org/intellij/sdk/language/parser/sd.bnf'
  targetRoot 'src/main/gen'
  pathToParser 'org/intellij/sdk/language/parser/SdParser.java'
  pathToPsiRoot 'org/intellij/sdk/language/psi/'
  purgeOldFiles true
}

compileJava {
  dependsOn generateSdLexer
  dependsOn generateSdParser
}


group 'org.yahoo.native'
version '1.0.1'

sourceCompatibility = 11

// This "noinspection" comment below is here to fix a warning
//noinspection GroovyAssignabilityCheck
repositories {
  mavenCentral()
}


sourceSets.main.java.srcDirs 'src/main/gen'

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
  version = '2021.2'
  plugins = ['com.intellij.java']
}

buildSearchableOptions {
  enabled = false
}

patchPluginXml {
  version = project.version
  sinceBuild = '203'
  untilBuild = '212.*'
  // in changeNotes you can add a description of the changes in this version (would appear in the plugin page in preferences\plugins)
    changeNotes = """
      <em></em>"""
}

test {
  useJUnitPlatform()
}